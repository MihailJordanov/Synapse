class_name MapLevelController
extends Node2D

@export_category("Go to scenes")
@export_file("*.tscn") var collection_scene_path: String = "res://Scenes/Scenes_In_Game/collection.tscn"
@export_file("*.tscn") var tutorial: String = "res://Scenes/Scenes_In_Game/Levels/tutorial.tscn"


@export_category("Button -> level")
@export var buttons: Array[NodePath] = []          
@export var level_ids: Array[String] = []       

@onready var panel: Control = $CanvasLayer/SelectLevelPanel2
@onready var cooler_texture_rect: TextureRect = $CanvasLayer/SelectLevelPanel2/RichTextLabel2/CoolerTextureRect
@onready var enemy_name_label: RichTextLabel = $CanvasLayer/SelectLevelPanel2/NameLabel
@onready var enemy_texture_rect: TextureRect = $CanvasLayer/SelectLevelPanel2/EnemyTextureRect
@onready var play_button: Button = $CanvasLayer/SelectLevelPanel2/Button
@onready var animation_player: AnimationPlayer = $AnimationPlayer
@onready var info_rich_text_label: RichTextLabel = $CanvasLayer/SelectLevelPanel2/InfoRichTextLabel
@onready var go_to_collection: Button = $CanvasLayer/DownPanel/GoToCollection
@onready var show_coins_label: RichTextLabel = $CanvasLayer/SelectLevelPanel2/ShowCoinsLabel

var _selected_button: LevelButton = null
var _pending_show := false
var _next_button: LevelButton = null
var _panel_visible := false

func _ready() -> void:
	if not LevelManager.is_cleared("0"):
		get_tree().call_deferred("change_scene_to_file", tutorial)
		return

	
	
	animation_player.play("openScene")
	# 1) Ð¡Ð¸Ð½Ñ…Ñ€Ð¾Ð½Ð¸Ð·Ð¸Ñ€Ð°Ð½Ðµ Ð½Ð° Ð±ÑƒÑ‚Ð¾Ð½Ð¸ Ñ LevelManager
	if buttons.size() != level_ids.size():
		push_warning("buttons Ð¸ level_ids Ñ‚Ñ€ÑÐ±Ð²Ð° Ð´Ð° ÑÐ° Ñ ÐµÐ´Ð½Ð°ÐºÐ²Ð° Ð´ÑŠÐ»Ð¶Ð¸Ð½Ð°! Ð˜Ð·Ð¿Ð¾Ð»Ð·Ð²Ð°Ð¼ Ð¼Ð¸Ð½Ð¸Ð¼Ð°Ð»Ð½Ð°Ñ‚Ð°.")

	var count := mini(buttons.size(), level_ids.size())
	for i in count:
		var btn := get_node_or_null(buttons[i]) as LevelButton
		if btn == null:
			push_warning("Ð•Ð»ÐµÐ¼ÐµÐ½Ñ‚ %d Ð² 'buttons' Ð½Ðµ Ðµ Ð²Ð°Ð»Ð¸Ð´ÐµÐ½ LevelButton." % i)
			continue


		var level_id : String = level_ids[i]
		btn.is_unlock = LevelManager.is_unlocked(level_id)
		btn.is_clear  = LevelManager.is_cleared(level_id)
		btn._is_visible = LevelManager.is_visible(level_id)
		if btn.has_method("_update_textures"):
			btn._update_textures()
	
		if btn.is_clear:
			for unlcok_level in btn.config.levels_to_unlock_on_win:
				if not LevelManager.is_unlocked(unlcok_level):
					LevelManager.add_unlocked(unlcok_level)
			
			for visible_level in btn.config.levels_to_visible_on_win:
				if not LevelManager.is_visible(visible_level):
					LevelManager.add_visible(visible_level)

		# 2) Ð’Ñ€ÑŠÐ·ÐºÐ°: Ð½Ð°Ñ‚Ð¸ÑÐºÐ°Ð½Ðµ Ð½Ð° Ð±ÑƒÑ‚Ð¾Ð½ -> ÑÐµÐ»ÐµÐºÑ†Ð¸Ñ/Ð¿Ð°Ð½ÐµÐ»
		btn.pressed.connect(func(): _on_level_button_pressed(btn))

	# 3) ÐÐ½Ð¸Ð¼Ð°Ñ†Ð¸Ð¸ Ð¸ Play
	animation_player.animation_finished.connect(_on_anim_finished)
	play_button.pressed.connect(_on_play_pressed)

	# 4) ÐŸÑŠÑ€Ð²Ð¾Ð½Ð°Ñ‡Ð°Ð»Ð½Ð¾ Ð¿Ð°Ð½ÐµÐ»ÑŠÑ‚ Ð´Ð° Ðµ ÑÐºÑ€Ð¸Ñ‚ (Ð°ÐºÐ¾ Ð°Ð½Ð¸Ð¼Ð°Ñ†Ð¸ÑÑ‚Ð° Ð¾Ñ‡Ð°ÐºÐ²Ð°)
	panel.visible = false
	_panel_visible = false


func _on_level_button_pressed(btn: LevelButton) -> void:
	
	if not btn.is_unlock:
		btn.modulate = Color(1, 0.3, 0.3) 
		var tween := create_tween()
		tween.tween_property(btn, "modulate", Color(1, 1, 1), 0.5)
		return
	
	if _panel_visible and _selected_button == btn:
		_apply_button_info(btn)
		return


	if _panel_visible and _selected_button != btn:
		_next_button = btn
		_pending_show = true
		animation_player.play("hide_select_level_menu")
		return


	_apply_button_info(btn)
	_selected_button = btn
	_show_panel()


func _apply_button_info(btn: LevelButton) -> void:
	# enemy_name_label = LevelButton.level_name
	enemy_name_label.bbcode_enabled = true
	enemy_name_label.clear()
	enemy_name_label.append_text(btn.level_name)  

	show_coins_label.text = "[b][color=#222222]Possible reward:[/color][/b] [b][color=gold]ðŸ’°[outline_size=2] $%dâ€“$%d" % [btn.config.coins_min, btn.config.coins_max]

	cooler_texture_rect.visible = btn.make_cooler


	enemy_texture_rect.texture = btn.config.enemy_texture


func _show_panel() -> void:
	panel.visible = true
	animation_player.play("show_select_level_menu")
	_panel_visible = true


func _hide_panel() -> void:
	if _panel_visible:
		animation_player.play("hide_select_level_menu")


func _on_anim_finished(_name: StringName) -> void:
	if _name == "hide_select_level_menu":
		panel.visible = false
		_panel_visible = false
		# ÐÐºÐ¾ Ñ‡Ð°ÐºÐ°Ð¼Ðµ Ð´Ð° Ð¿Ð¾ÐºÐ°Ð¶ÐµÐ¼ Ð½Ð¾Ð² Ð¸Ð·Ð±Ð¾Ñ€ â€“ ÑÐµÐ³Ð° Ð³Ð¾ Ð¿Ñ€Ð°Ð²Ð¸Ð¼
		if _pending_show and _next_button:
			_apply_button_info(_next_button)
			_selected_button = _next_button
			_next_button = null
			_pending_show = false
			_show_panel()
	elif _name == "show_select_level_menu":
		panel.visible = true
		_panel_visible = true


func _on_play_pressed() -> void:
	if _selected_button == null:
		return
	if CollectionManager.deck.size() < 5:
		info_rich_text_label.text = "Your deck must contain at least 5 cards."
		_pulse_button(go_to_collection)
		return

	LevelRuntime.config = _selected_button.config.duplicate(true) 
	animation_player.play("closeScene")
	get_tree().change_scene_to_file("res://Scenes/Scenes_In_Game/Levels/level_00.tscn") 




func _unhandled_input(event: InputEvent) -> void:
	# ÐšÐ»Ð¸Ðº Ð¸Ð·Ð²ÑŠÐ½ Ð¿Ð°Ð½ÐµÐ»Ð° Ð¸ Ð¸Ð·Ð²ÑŠÐ½ Ð±ÑƒÑ‚Ð¾Ð½Ð¸Ñ‚Ðµ -> hide
	if event is InputEventMouseButton and event.pressed and event.button_index == MOUSE_BUTTON_LEFT:
		if not _panel_visible:
			return
		var hovered := get_viewport().gui_get_hovered_control()

		# ÐÐºÐ¾ Ð½ÑÐ¼Ð° hovered control -> ÑÐºÑ€Ð¸Ð²Ð°Ð¼Ðµ
		if hovered == null:
			_hide_panel()
			return

		# ÐÐºÐ¾ Ðµ Ð² Ð¿Ð°Ð½ÐµÐ»Ð° -> Ð½Ðµ Ð¿Ñ€Ð°Ð²Ð¸Ð¼ Ð½Ð¸Ñ‰Ð¾
		if _is_descendant_of(hovered, panel):
			return

		# ÐÐºÐ¾ Ðµ Ð²ÑŠÑ€Ñ…Ñƒ Ð½ÑÐºÐ¾Ð¹ Ð¾Ñ‚ Ð±ÑƒÑ‚Ð¾Ð½Ð¸Ñ‚Ðµ -> Ð½Ðµ ÐºÑ€Ð¸Ð¹ (Ð¾Ð½Ð·Ð¸ handler Ñ‰Ðµ ÑÐµ Ð¿Ð¾Ð³Ñ€Ð¸Ð¶Ð¸)
		for p in buttons:
			var b := get_node_or_null(p) as Control
			if b and _is_descendant_of(hovered, b):
				return

		# Ð˜Ð½Ð°Ñ‡Ðµ â€“ ÐºÐ»Ð¸Ðº Ð²ÑÑ‚Ñ€Ð°Ð½Ð¸
		_hide_panel()


func _is_descendant_of(node: Control, ancestor: Node) -> bool:
	var n: Node = node
	while n:
		if n == ancestor:
			return true
		n = n.get_parent()
	return false


func _on_go_to_collection_button_down() -> void:
	animation_player.play("closeScene")
	await animation_player.animation_finished
	get_tree().change_scene_to_file(collection_scene_path)
	
func _pulse_button(button: Button) -> void:
	if not is_instance_valid(button):
		return

	button.pivot_offset = button.size * 0.5 

	var tween := create_tween()
	tween.set_trans(Tween.TRANS_SINE).set_ease(Tween.EASE_IN_OUT)
	tween.parallel().tween_property(button, "scale", Vector2(1.2, 1.2), 0.05)
	tween.parallel().tween_property(button, "modulate", Color(1.3, 1.3, 1.3), 0.05)
	tween.tween_property(button, "scale", Vector2(1, 1), 0.05)
	tween.parallel().tween_property(button, "modulate", Color(1, 1, 1), 0.05)


func _on_go_to_tutorial_button_down() -> void:
	animation_player.play("closeScene")
	await animation_player.animation_finished
	get_tree().change_scene_to_file(tutorial)
	
